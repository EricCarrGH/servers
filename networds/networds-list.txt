10 DIM BASE$(128),REQ$(1024),URL$(1024),RESP$(1024),RES$(1),DAT1$(1024),DAT2$(1024),DAT3$(1024)
20 BASE$="N:HTTP://www.billsgames.com/fujinet/networds/game.php?cmd="
30 DIM NAMEME$(40),NAMETHEM$(40),LTRS$(9),GAMEID$(40)
40 DIM WORD$(9),WORDS$(250)
50 SCOREME=0:SCORETHEM=0:ROUND=1:MYPLY=2

100 GRAPHICS 0:POKE 82,0:POKE 83,39:POKE 709,15:POKE 710,0:? CHR$(125);"Networds"

200 ? "Name: ";:INPUT NAMEME$
210 REQ$="n&name=":REQ$(LEN(REQ$)+1)=NAMEME$:GOSUB 30000:GOSUB 31000:CLOSE #1
220 IF RES$="W" THEN MYPLY=1:GOSUB 10000
230 IF RES$<>"M" THEN 32000

300 GAMEID$=DAT1$
310 NAMETHEM$=DAT2$
320 LTRS$=DAT3$
330 ? "You are player ";MYPLY;" versus":? NAMETHEM$

400 ? "Letters: ";LTRS$
410 WORDS$=""
420 ? "Word [blank to end]";:INPUT WORD$:IF WORD$="" THEN 450
430 WORDS$(LEN(WORDS$)+1)="+":WORDS$(LEN(WORDS$)+1)=WORD$
440 GOTO 420
450 ? "Words you entered:":? WORDS$

500 REQ$="w"
510 REQ$(2)="&ply=":REQ$(LEN(REQ$)+1)=CHR$(48+MYPLY)
520 REQ$(LEN(REQ$)+1)="&id=":REQ$(LEN(REQ$)+1)=GAMEID$
530 REQ$(LEN(REQ$)+1)="&words=":REQ$(LEN(REQ$)+1)=WORDS$

600 GOSUB 30000
610 GOSUB 31000:IF RES$="X" THEN 660
620 IF RES$<>"S" THEN 32000
630 ? DAT1$;" -- ";DAT2$
640 IF DAT2$(1,1)>="0" AND DAT2$(1,1)<="9" THEN SCOREME=SCOREME+VAL(DAT2$)
650 GOTO 610
660 CLOSE #1:? :? "Score: ";SCOREME

9999 END

10000 REM Wait for opponent
10010 GAMEID$=DAT1$
10020 ? "Waiting for opponent... (game id=";GAMEID$;")"
10030 FOR I=0 TO 1000:NEXT I
10040 REQ$="m&id=":REQ$(6)=GAMEID$:GOSUB 30000:GOSUB 31000
10050 IF RES$="W" THEN 10020
10060 RETURN

30000 REM TRAP 30100
30010 CLOSE #1:URL$=BASE$:URL$(LEN(BASE$)+1)=REQ$:OPEN #1,4,2,URL$
30015 ? "<";URL$;">":?
30020 RETURN

30100 ? "Error! ";PEEK(195)
30110 END

31000 INPUT #1,RESP$
31010 RES$=RESP$(1,1)
31020 DAT1$=RESP$(3):FOUND=0
31030 FOR I=1 TO LEN(DAT1$):IF DAT1$(I,I)=";" THEN FOUND=I:I=999
31040 NEXT I
31050 IF FOUND=0 THEN DAT2$="":DAT3$="":RETURN
31060 DAT2$=DAT1$(FOUND+1):DAT1$(FOUND)="":FOUND =0
31070 FOR I=1 TO LEN(DAT2$):IF DAT2$(I,I)=";" THEN FOUND=I:I=999
31080 NEXT I
31090 IF FOUND=0 THEN DAT3$="":RETURN
31100 DAT3$=DAT2$(FOUND+1):DAT2$(FOUND)=""
31110 RETURN

32000 ? "Unexpected response: ";RESP$
32010 END
